#!/usr/bin/env perl 
# -*- cperl -*-

package RUM::Script::Indexes;

=head1 NAME

rum_indexes - List, install, or remove RUM indexes

=head1 SYNOPSIS

Get a list of all available indexes:

  $ ./bin/rum_indexes -l
  The following indexes are available. Indexes that you already
  have are marked with '*':

    1     hg19:                human (Homo sapiens)
  * 2     hg18:                human (Homo sapiens)
    3      mm9:                mouse (Mus musculus)
    4  danRer7:            zebrafish (Danio rerio)
  --- snip ---

Or filtered by a regular expression:

  $ ./bin/rum_indexes -l human
  The following indexes are available. Indexes that you already 
  have are marked with '*':

    1     hg19:                human (Homo sapiens)
  * 2     hg18:                human (Homo sapiens)

Install an index by build name:

  $ rum-indexes -i hg19

or by index number:

  $ rum-indexes -i 1

Remove an index by build name or number

  $ rum-indexes -r hg19
  $ rum-indexes -r 1
  
=cut

use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/../lib";
use RUM::Repository;
use RUM::Script qw(get_options show_usage);
use POSIX;

sub new {
    my ($class) = @_;
    return bless {}, $class;
}

sub main {
    my ($self) = @_;

    get_options(
        "--list|l"    => \(my $do_list),
        "--install|i" => \(my $do_install),
        "--remove|r"  => \(my $do_remove));
    
    show_usage() unless $do_list || $do_install || $do_remove;

    $self->{repo} = RUM::Repository->new;
    
    if ($do_list) {
        $self->list_indexes(@ARGV);
    }
    elsif ($do_install) {
        $self->install_index(@ARGV);
    }
    elsif ($do_remove) {
        $self->remove_index(@ARGV);
    }
}

sub repo { shift->{repo} }

sub list_indexes {
    my ($self, $pattern) = @_;
    my $repo = $self->repo;
    
    $repo->fetch_organisms_file unless -e $repo->organisms_file;

    print <<EOF;
The following indexes are available. Indexes that you already have are marked with '*':

EOF
    my %query;
    $query{pattern} = qr /$pattern/ if $pattern;
    $self->print_indexes($repo->indexes(%query));
}

sub print_indexes {
    my ($self, @indexes) = @_;

    my $i = 1;
    for my $idx (@indexes) {
        my $have = $self->repo->has_index($idx) ? "*" : " ";
        printf "%1s %2d %8s: %20s (%s)\n", 
            $have, $idx->order, $idx->build, $idx->common, $idx->latin;
    }
}

sub select_index {
    my ($self, $spec) = @_;

    my $repo = $self->repo;
    my @orgs = $repo->indexes;
    if (isdigit($spec)) {
        my @indexes = $repo->indexes;
        return $indexes[$spec - 1] if $spec > 0 && $spec <= @indexes;
    }
    
    my @candidates = $repo->indexes(pattern => $spec);
    return $candidates[0] if @candidates == 1;
    
    if (@candidates > 1) {
        print <<EOF;
I found more than one index matching '$spec'; please be more specific:

EOF
        $self->print_indexes(@candidates);
        exit(1);
    }

    if (@candidates == 0) {
        print <<EOF;

I don't have an index with a build name, common name, or latin name of
$spec. Please try $0 --list to see available indexes.
EOF
        exit(1);
    }
    
}

sub install_index {
    my ($self, $spec) = @_;
    unless ($spec) {
        die <<EOF;
Please specify an index. Use $0 -l to see a list of available indexes.
EOF
    }
    my $org = $self->select_index($spec);
    print "Installing $org->{build}: $org->{common} ($org->{latin})\n";
    my $callback = sub {
        my ($event, $url) = @_;
        print "$url\n" if $event eq 'start';
    };
    $self->repo->install_index($org, $callback);
}

sub remove_index {
    my ($self, $spec) = @_;
    my $org = $self->select_index($spec);
    print "Removing $org->{build}: $org->{common} ($org->{latin})\n";
    my $callback = sub {
        my ($event, $file) = @_;
        print "  Removing $file\n" if $event eq 'start';
    };
    $self->repo->remove_index($org, $callback);
}

__PACKAGE__->new->main;
